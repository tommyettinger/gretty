apply plugin: 'groovy'

import org.apache.tools.ant.filters.*

dependencies {
  implementation gradleApi()
  implementation "commons-configuration:commons-configuration:$commons_configuration_version"
  implementation "de.undercouch:gradle-download-task:$gradle_download_task_version"
  implementation "org.gretty:gretty:$grettyVersion"
}

def projectProps = [
  projectGroup: project.group,
  projectName: project.name,
  projectVersion: project.version,
  buildDate: new Date().format('yyyy-MM-dd'),
  buildUser: System.getProperty('user.name'),
  buildOrigin: ('hostname'.execute().text.trim()),
  gebVersion: project.gebVersion,
  geckoDriverVersion: project.geckoDriverVersion,
  geckoDriverPlatform: project.geckoDriverPlatform,
  seleniumVersion: project.seleniumVersion,
  spock_version: project.spock_version,
  testAllContainers: project.testAllContainers
]

processResources {
  inputs.properties projectProps
  outputs.upToDateWhen{ false }
  filesMatching('**/project.properties') {
    filter ReplaceTokens, tokens: projectProps
  }
}
